plugins {
    id 'java'
    id 'maven'
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

//from now on this is how you'll change version
def versionObj = new Version(major: 1, minor: 1, patch: 1, state: 'RELEASE')

group 'us.eunoians'
version = "${versionObj.toString()}"


sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.16.1-R0.1-SNAPSHOT'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    testCompile group: 'org.powermock', name: 'powermock-mockito-release-full', version: '1.5.4', ext: 'pom'

}

// Run JUnit tests
test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand 'pluginVersion': version
        include 'plugin.yml'
    }
}

shadowJar {
    archiveName = "Prisma-${version}.jar"
}

artifacts {
    archives shadowJar
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

class Version {

    String major, minor, patch, state, full

    String getFullVersion() {
        full = "$major.$minor.$patch-$state"
        return full
    }

    String toString() {
        getFullVersion()
    }
}
