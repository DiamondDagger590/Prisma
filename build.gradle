plugins {
    id 'java'
    id 'maven'
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

//from now on this is how you'll change version
def versionObj = new Version(major: 1, minor: 4, patch: 0, fix: 1, state: 'BETA-SNAPSHOT')

group 'us.eunoians'
version = "${versionObj.toString()}"


sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigot mc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand 'pluginVersion': version
        include 'plugin.yml'
    }
}

shadowJar {
    archiveName = "McRPG-${version}.jar"
    relocate 'de.tr7zw.changeme.nbtapi', 'us.eunoians.mcrpg.nbtapi'
    relocate 'org.slf4j', 'us.eunoians.mcrpg'
    relocate 'org.bstats', 'us.eunoians.mcrpg'
}

artifacts {
    archives shadowJar
}

lombok {
    version = "1.18.4"
    sha256 = ""
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

class Version {

    String major, minor, patch, fix, state, full

    String getFullVersion() {
        full = "$major.$minor.$patch.$fix-$state"
        return full
    }

    String toString() {
        getFullVersion()
    }
}
